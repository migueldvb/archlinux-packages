# Maintainer: Miguel de Val-Borro <valborro at princeton dot edu>

pkgname=('python-django-markdown-deux' 'python2-django-markdown-deux')
pkgver=1.0.4
pkgrel=1
pkgdesc="A django app that provides Markdown-related template tags"
arch=('any')
url="http://github.com/trentm/django-markdown-deux"
license=('BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=("https://github.com/trentm/django-markdown-deux/archive/${pkgver}.tar.gz")
md5sums=('8a3e4ccf159b4f89e54f8c80b4374925')

build() {
  cp -r ${srcdir}/django-markdown-deux-${pkgver} ${srcdir}/django-markdown-deux-${pkgver}-py2

  cd ${srcdir}/django-markdown-deux-${pkgver}
  python setup.py build

  cd ${srcdir}/django-markdown-deux-${pkgver}-py2
  python2 setup.py build
}

package_python-django-markdown-deux() {
  depends=('python-django')
  cd ${srcdir}/django-markdown-deux-${pkgver}
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  python setup.py install --prefix=/usr --root=${pkgdir}
}

package_python2-django-markdown-deux() {
  depends=('python2-django')
  cd ${srcdir}/django-markdown-deux-${pkgver}-py2
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  python2 setup.py install --prefix=/usr --root=${pkgdir}
}
