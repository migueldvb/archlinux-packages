# Maintainer: Miguel de Val-Borro <miguel at archlinux dot net>

pkgname=('python-markdown2' 'python2-markdown2')
pkgver=2.3.0
pkgrel=1
pkgdesc="A django app that provides Markdown-related template tags"
arch=('any')
url="http://github.com/trentm/python-markdown2"
license=('BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=("https://github.com/trentm/python-markdown2/archive/${pkgver}.tar.gz")
md5sums=('2d8ad801ba6c38b4a1698b2e1bcbcf5c')

build() {
  cp -r ${srcdir}/python-markdown2-${pkgver} ${srcdir}/python-markdown2-${pkgver}-py2

  cd ${srcdir}/python-markdown2-${pkgver}
  python setup.py build

  cd ${srcdir}/python-markdown2-${pkgver}-py2
  python2 setup.py build
}

package_python-markdown2() {
  depends=('python-django')
  cd ${srcdir}/python-markdown2-${pkgver}
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  python setup.py install --prefix=/usr --root=${pkgdir}
}

package_python2-markdown2() {
  depends=('python2-django')
  cd ${srcdir}/python-markdown2-${pkgver}-py2
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  python2 setup.py install --prefix=/usr --root=${pkgdir}
}
